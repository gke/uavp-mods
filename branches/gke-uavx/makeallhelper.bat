@echo off

SETLOCAL ENABLEDELAYEDEXPANSION

rem Helper script for makeall.bat
rem =======================================================
rem parameters passed are:

set	VERSION=%1
set	CLOCK=%2
set PROC=%3
set DBG=%4
set RX=%5
set CFG=%6

for /f "tokens=2-4 delims=/ " %%a in ('date /T') do set year=%%c
for /f "tokens=2-4 delims=/ " %%a in ('date /T') do set month=%%a
for /f "tokens=2-4 delims=/ " %%a in ('date /T') do set day=%%b
set TODAY=%year%%month%%day%

for /f "tokens=1 delims=: " %%h in ('time /T') do set hour=%%h
for /f "tokens=2 delims=: " %%m in ('time /T') do set minutes=%%m
for /f "tokens=3 delims=: " %%a in ('time /T') do set ampm=%%a
set NOW=%hour%%minutes%%ampm%

set CSRC=accel adc uavx irq menu control compass_altimeter tests serial utils gps autonomous i2c outputs
set ASRC=bootl18f

set CC="C:\MCC18\bin\mcc18" 
set CCMD=  -Oi -w1 -Opa- -DBATCHMODE

set ACMD=/q /d%CLOCK% /p%PROC% %%i.asm /l%%i.lst /e%%i.err /o%%i.o
set AEXE="C:\MCC18\mpasm\mpasmwin.exe"

set LCMD=/p%PROC% /l"C:\MCC18\lib" /k"C:\MCC18\lkr"
set LEXE="C:\MCC18\bin\mplink.exe"

rem Set all the name tokens for the HEX files
set G=
set E=
set D=
set T=
set R=
set B=
set C=
rem if "%DBG%"   == "DEBUG_MOTORS"      set D=-Debug_MOTORS
if "%DBG%"   == "DEBUG_SENSORS"     set D=-Debug_SENSORS
if "%CFG%"    == "TRICOPTER"           set C=-TRI
if "%RX%" == "RX6CH"	set R=-6CH
if "%CLOCK%"    == "CLOCK_16MHZ"           set X=-16
if "%CLOCK%"    == "CLOCK_40MHZ"           set X=-40

rem Build the list of expected object files
set F=
for %%i in ( %CSRC% ) do set F=!F! %%i.o
for %%i in ( %ASRC% ) do set F=!F! %%i.o


for %%i in ( %CSRC% ) do %CC% -p=%PROC% /i"C:\MCC18\h" %%i.c -fo=%%i.o %CCMD%  -D%CLOCK% -D%DBG% -D%RX% -D%CFG% >> log.lst

for %%i in ( %ASRC% ) do %AEXE%  %ACMD% >> log.lst

%LEXE% %LCMD% %F% /u_CRUNTIME /z__MPLAB_BUILD=1 /W /o UAVX-V1.744gke-%PROC%%X%%R%%C%%D%%T%.hex >> log.lst 


if %ERRORLEVEL% == 1 goto FAILED

echo compiled - UAVX-V1.744gke-%PROC%%X%%R%%C%%D%%T%.hex
echo compiled - UAVX-V1.744gke-%PROC%%X%%R%%C%%D%%T%.hex >> gen.lst
call makeclean.bat
goto FINISH

:FAILED
echo failed - UAVX-V1.744gke-%PROC%%X%%R%%C%%D%%T%.hex
echo failed - UAVX-V1.744gke-%PROC%%X%%R%%C%%D%%T%.hex >> gen.lst
rem don't delete working files

:FINISH















